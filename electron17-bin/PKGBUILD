# Maintainer: Nicola Squartini <tensor5@gmail.com>

_pkgname=electron17
pkgname=electron17-bin
pkgver=17.4.7
pkgrel=1
pkgdesc='Build cross platform desktop apps with web technologies'
arch=('x86_64' 'loong64')
url='https://electronjs.org/'
license=('MIT' 'custom')
depends=('c-ares' 'ffmpeg' 'gtk3' 'libevent' 'libxslt' 'minizip' 'nss' 're2'
         'snappy')
optdepends=('kde-cli-tools: file deletion support (kioclient5)'
            'libappindicator-gtk3: StatusNotifierItem support'
            'pipewire: WebRTC desktop sharing under Wayland'
            'trash-cli: file deletion support (trash-put)'
            "xdg-utils: open URLs with desktop's default (xdg-email, xdg-open)")
conflicts=("electron17")
provides=("electron17" "electron")
source=("http://public.loongarch.dev/sources/electron/electron-v${pkgver}-linux-loong64.zip/151bd8bc43b5b922b0751d61e8c6e8a2/electron-v${pkgver}-linux-loong64.zip"
        "icon.png::http://public.loongarch.dev/sources/electron/electron17.png/5fbf91dbab04c5f2b19e1aca8f87a307/electron17.png"
        "electron-launcher.sh"
        "electron.desktop"
       )
noextract=("electron-v${pkgver}-linux-loong64.zip")
sha256sums=('0297e6e75442877bacee767a1fa3b7a2c0c07e71caf8cbae73051bc660024d19'
            'b0f6f06d7e94053dabcc7dd8adc83d925089dbca55df0ca573b267b710e1f79d'
            '23e4917339511ef204c8eed70598f284a4a6ee5cd8ee1f5205020c259e839fcc'
            'beaeaa6c81c95c9426c333700294c75a79ba043cba8f7a5bb6750b6789387885')

package() {
  install -dm755 "${pkgdir:?}/usr/lib/${_pkgname}"
  bsdtar -xf electron-v${pkgver}-linux-loong64.zip -C "${pkgdir}/usr/lib/${_pkgname}"

  chmod u+s "${pkgdir}/usr/lib/${_pkgname}/chrome-sandbox"

  install -dm755 "${pkgdir}/usr/share/licenses/${_pkgname}"
  for l in "${pkgdir}/usr/lib/${_pkgname}"/{LICENSE,LICENSES.chromium.html}; do
    ln -s  \
      "$(realpath --relative-to="${pkgdir}/usr/share/licenses/${_pkgname}" "${l}")" \
      "${pkgdir}/usr/share/licenses/${_pkgname}"
  done

  install -Dm755 "${srcdir}/electron-launcher.sh" \
    "${pkgdir}/usr/bin/${_pkgname}"
  if [[ "${_use_suffix}" == 0 ]]; then
    ln "${pkgdir}/usr/bin/${_pkgname}" \
      "${pkgdir}/usr/bin/${_pkgname}${_major_ver}"
  fi

  # Install .desktop and icon file (see default_app-icon.patch)
  install -Dm644 electron.desktop \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 icon.png \
          "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"  # hicolor has no 1024x1024
}
