# Maintainer: Rustmilian <#####[at]#####[dot]org>
# https://github.com/endeavouros-team/PKGBUILDS/blob/master/calamares/PKGBUILD

pkgname=calamares
pkgver=11779.c375c251d
pkgrel=1
pkgdesc='Distribution-independent installer framework'
arch=(loong64 x86_64)
url="https://calamares.io/"
license=('GPL3')
depends=(
  'ckbcomp'
  'cryptsetup'
  'dmidecode'
  'doxygen'
  'gptfdisk'
  'hwinfo'
  'kconfig'
  'kcoreaddons'
  'ki18n'
  'kparts'
  'kpmcore'
  'kservice'
  'kwidgetsaddons'
  'libpwquality'
  'networkmanager'
  'polkit-qt6'
  'pybind11'
  'python'
  'qt6-declarative'
  'qt6-svg'
  'qt6-tools'
  'qt6-webengine'
  'rsync'
  'solid'
  'squashfs-tools'
  'upower'
  'yaml-cpp'
)
makedepends=(
  'cmake'
  'extra-cmake-modules'
  'gawk'
  'git'
  'python-jsonschema'
  'python-pyaml'
  'python-unidecode'
)

options=(!strip !emptydirs)
source=("git+https://github.com/archloong/calamares.git#branch=calamares-la64"
	"calamares.desktop"
	"calamares_polkit"
	"49-nopasswd-calamares.rules"
	)
sha256sums=('SKIP'
            '05c4ee3398ebc5168332888f83e49baf9e143641f11873eed14fcd2b6e409a54'
            '71f74f23f19f828c2f9051f0616a347e632b6d12387f5b1095b6696ed2ce37f2'
            '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176')
pkgver() {
  cd ${srcdir}/${pkgname}
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd ${srcdir}/${pkgname}
    sed -i -e "s|\${CALAMARES_VERSION_MAJOR}.\${CALAMARES_VERSION_MINOR}.\${CALAMARES_VERSION_PATCH}|${pkgver}-${pkgrel}|g" CMakeLists.txt
    sed -i -e "s|CALAMARES_VERSION_RC 1|CALAMARES_VERSION_RC 0|g" CMakeLists.txt
#    # Update branding.desc with the proper values
#    replace_command='
#    {
#        gsub(/\${version}/,version);
#        gsub(/\${release_name}/,release);
#        print
#    }
#    '
#    awk -i inplace -v version="$(pkgver)" -v release="${release_name}" "$replace_command" "${srcdir}/calamares/data/eos/branding/endeavouros/branding.desc"
}

build() {
    cmake -B build -S "${srcdir}/calamares" \
    -DWITH_QT6=ON \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_CONFIG=OFF \
    -DSKIP_MODULES="dracut \
    dummycpp dummyprocess dummypython dummypythonqt \
    finishedq initcpio keyboardq license localeq notesqml oemid \
    openrcdmcryptcfg plymouthcfg plasmalnf services-openrc \
    summaryq tracking usersq webview welcomeq"
    export DESTDIR="$srcdir/build"
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
    install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
    install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
    chmod 750 "$pkgdir"/etc/polkit-1/rules.d
}
